apply plugin: 'java'
//apply plugin: 'maven'

group = 'com.micromux'
version = '2.1.1-SNAPSHOT'

description = """Twig JDBC driver for Cassandra/CQL"""

test.outputs.upToDateWhen { false }

sourceCompatibility = 1.5
targetCompatibility = 1.5

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.0'
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version:'2.1.9'
    compile group: 'com.google.guava', name: 'guava', version:'18.0'
    testCompile group: 'junit', name: 'junit', version:'4.10'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.7.2'
    testCompile group: 'org.cassandraunit', name: 'cassandra-unit', version:'2.1.9.2'
    testCompile group: 'io.netty', name: 'netty-all', version: '4.0.27.Final'
    testCompile group: 'net.ju-n.compile-command-annotations', name: 'compile-command-annotations', version: '1.0.3'
}

tasks.withType(Test) {
    systemProperty 'host', System.getProperty('host', 'localhost')
    systemProperty 'port', System.getProperty('port', '19042')
}

test {
    afterTest { desc, result ->
        println "Completed ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

/**
 * Create single distribution JAR with all dependencies baked in.
 */
task jarAll(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task distribution(dependsOn: ['clean', 'test', 'jar', 'jarAll'])